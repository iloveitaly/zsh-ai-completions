#compdef localias

function _localias {
  local context state state_descr line
  typeset -A opt_args

  _arguments -C -S \
    '(-c --configfile)'{-c,--configfile}'[path to the configuration file to edit]:config file:_files' \
    '(-h --help)'{-h,--help}'[help for localias]' \
    '(-v --version)'{-v,--version}'[version for localias]' \
    '1: :_localias_commands' \
    '*:: :->args'

  case $state in
    (args)
      case $words[1] in
        (clear)
          _arguments -S : '(-h --help)'{-h,--help}'[help for clear]'
          ;;
        (help)
          _arguments -S : \
            '(-h --help)'{-h,--help}'[help for help]' \
            '1:command:_localias_commands'
          ;;
        (import)
          _arguments -S : \
            '(-h --help)'{-h,--help}'[help for import]' \
            '*:config file:_files'
          ;;
        (list)
          _arguments -S : '(-h --help)'{-h,--help}'[help for list]'
          ;;
        (rm)
          _arguments -S : \
            '(-h --help)'{-h,--help}'[help for rm]' \
            '1:alias name:'
          ;;
        (run)
          _arguments -S : '(-h --help)'{-h,--help}'[help for run]'
          ;;
        (set)
          _arguments -S : \
            '(-h --help)'{-h,--help}'[help for set]' \
            '1:domain:' \
            '2:port:'
          ;;
        (start)
          _arguments -S : '(-h --help)'{-h,--help}'[help for start]'
          ;;
        (status)
          _arguments -S : '(-h --help)'{-h,--help}'[help for status]'
          ;;
        (stop)
          _arguments -S : '(-h --help)'{-h,--help}'[help for stop]'
          ;;
        (version)
          _arguments -S : '(-h --help)'{-h,--help}'[help for version]'
          ;;
        (*)
          _default
          ;;
      esac
      ;;
  esac
}

function _localias_commands {
  local -a commands
  commands=(
    'clear:clear all aliases'
    'help:Help about any command'
    'import:import all aliases from one or more other config files'
    'list:list all aliases'
    'rm:remove an alias'
    'run:run the proxy server in the foreground'
    'set:add or edit an alias'
    'start:start the proxy server as a daemon process'
    'status:show the status of the daemon process'
    'stop:stop the daemon process'
    'version:show the version of this binary'
  )
  _describe -t commands 'command' commands
}

_localias "$@"
