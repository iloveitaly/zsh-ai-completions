#compdef fzf

local -a args

args=(
  # Search
  '(-e --exact)'{-e,--exact}'[Enable exact-match]'
  '(+x --no-extended)'{+x,--no-extended}'[Disable extended-search mode]'
  '(-i --ignore-case)'{-i,--ignore-case}'[Case-insensitive match]'
  '(+i --no-ignore-case)'{+i,--no-ignore-case}'[Case-sensitive match]'
  '--smart-case[Smart-case match]'
  '--scheme=[Scoring scheme]:scheme:(default path history)'
  '(-n --nth)'{-n+,--nth=}'[Comma-separated list of field index expressions]:expressions'
  '--with-nth=[Transform the presentation of each line]:expressions'
  '--accept-nth=[Define which fields to print on accept]:expressions'
  '(-d --delimiter)'{-d+,--delimiter=}'[Field delimiter regex]:delimiter'
  '(+s --no-sort)'{+s,--no-sort}'[Do not sort the result]'
  '--literal[Do not normalize latin script letters]'
  '--tail=[Maximum number of items to keep in memory]:number'
  '--disabled[Do not perform search]'
  '--tiebreak=[Comma-separated list of sort criteria]:criteria:(length chunk pathname begin end index)'

  # Input/Output
  '--read0[Read input delimited by ASCII NUL characters]'
  '--print0[Print output delimited by ASCII NUL characters]'
  '--ansi[Enable processing of ANSI color codes]'
  '--sync[Synchronous search for multi-staged filtering]'

  # Global Style
  '--style=[Apply a style preset]:preset:(default minimal full)'
  '--color=[Base scheme and/or custom colors]:colspec'
  '--no-color[Disable colors]'
  '--no-bold[Do not use bold text]'

  # Display Mode
  '--height=[Display fzf window below the cursor with the given height]:height'
  '--min-height=[Minimum height when --height is given as a percentage]:height'
  '--tmux=[Start fzf in a tmux popup]::options'

  # Layout
  '--layout=[Choose layout]:layout:(default reverse reverse-list)'
  '--margin=[Screen margin]:margin'
  '--padding=[Padding inside border]:padding'
  '--border=[Draw border around the finder]::style:(rounded sharp bold block thinblock double horizontal vertical top bottom left right line none)'
  '--border-label=[Label to print on the border]:label'
  '--border-label-pos=[Position of the border label]:column'

  # List Section
  '(-m --multi)'{-m,--multi}'::[Enable multi-select]::max'
  '--highlight-line[Highlight the whole current line]'
  '--cycle[Enable cyclic scroll]'
  '--wrap[Enable line wrap]'
  '--wrap-sign=[Indicator for wrapped lines]:string'
  '--no-multi-line[Disable multi-line display of items when using --read0]'
  '--raw[Enable raw mode]'
  '--track[Track the current selection when the result is updated]'
  '--tac[Reverse the order of the input]'
  '--gap=[Render empty lines between each item]::n'
  '--gap-line=[Draw horizontal line on each gap using the string]::string'
  '--freeze-left=[Number of fields to freeze on the left]:n'
  '--freeze-right=[Number of fields to freeze on the right]:n'
  '--keep-right[Keep the right end of the line visible on overflow]'
  '--scroll-off=[Number of screen lines to keep above or below]:lines'
  '--no-hscroll[Disable horizontal scroll]'
  '--hscroll-off=[Number of screen columns to keep to the right]:cols'
  '--jump-labels=[Label characters for jump mode]:chars'
  '--gutter=[Character used for the gutter column]:char'
  '--gutter-raw=[Character used for the gutter column in raw mode]:char'
  '--pointer=[Pointer to the current line]:str'
  '--marker=[Multi-select marker]:str'
  '--marker-multi-line=[Multi-select marker for multi-line entries]:str'
  '--ellipsis=[Ellipsis to show when line is truncated]:str'
  '--tabstop=[Number of spaces for a tab character]:spaces'
  '--scrollbar=[Scrollbar character(s)]::chars'
  '--no-scrollbar[Hide scrollbar]'
  '--list-border=[Draw border around the list section]::style:(rounded sharp bold block thinblock double horizontal vertical top bottom left right none)'
  '--list-label=[Label to print on the list border]:label'
  '--list-label-pos=[Position of the list label]:column'

  # Input Section
  '--no-input[Disable and hide the input section]'
  '--prompt=[Input prompt]:str'
  '--info=[Finder info style]:style'
  '--info-command=[Command to generate info line]:command'
  '--separator=[Draw horizontal separator on info line using the string]:str'
  '--no-separator[Hide info line separator]'
  '--ghost=[Ghost text to display when the input is empty]:text'
  '--filepath-word[Make word-wise movements respect path separators]'
  '--input-border=[Draw border around the input section]::style:(rounded sharp bold block thinblock double horizontal vertical top bottom left right line none)'
  '--input-label=[Label to print on the input border]:label'
  '--input-label-pos=[Position of the input label]:column'

  # Preview Window
  '--preview=[Command to preview highlighted line]:command'
  '--preview-window=[Preview window layout]:opts'
  '--preview-border=[Draw border around the preview window]::style:(rounded sharp bold block thinblock double horizontal vertical top bottom left right line none)'
  '--preview-label=[Label to print on the preview border]:label'
  '--preview-label-pos=[Position of the preview label]:n'

  # Header
  '--header=[String to print as header]:str'
  '--header-lines=[The first N lines of the input are treated as header]:n'
  '--header-first[Print header before the prompt line]'
  '--header-border=[Draw border around the header section]::style:(rounded sharp bold block thinblock double horizontal vertical top bottom left right line none)'
  '--header-lines-border=[Display header from --header-lines with a separate border]::style'
  '--header-label=[Label to print on the header border]:label'
  '--header-label-pos=[Position of the header label]:column'

  # Footer
  '--footer=[String to print as footer]:str'
  '--footer-border=[Draw border around the footer section]::style:(rounded sharp bold block thinblock double horizontal vertical top bottom left right line none)'
  '--footer-label=[Label to print on the footer border]:label'
  '--footer-label-pos=[Position of the footer label]:column'

  # Scripting
  '(-q --query)'{-q+,--query=}'[Start the finder with the given query]:str'
  '(-1 --select-1)'{-1,--select-1}'[Automatically select the only match]'
  '(-0 --exit-0)'{-0,--exit-0}'[Exit immediately when there is no match]'
  '(-f --filter)'{-f+,--filter=}'[Print matches for the initial query and exit]:str'
  '--print-query[Print query as the first line]'
  '--expect=[Comma-separated list of keys to complete fzf]:keys'

  # Key/Event Binding
  '*--bind=[Custom key/event bindings]:bindings'

  # Advanced
  '--with-shell=[Shell command and flags to start child processes with]:str'
  '--listen=[Start HTTP server to receive actions]::address_port'

  # Directory Traversal
  '--walker=[Walker options]:opts'
  '--walker-root=[List of directories to walk]:dir:_dirs'
  '--walker-skip=[Comma-separated list of directory names to skip]:dirs'

  # History
  '--history=[File to store fzf search history]:file:_files'
  '--history-size=[Maximum number of entries to keep in the file]:n'

  # Shell Integration
  '--bash[Print script to set up Bash shell integration]'
  '--zsh[Print script to set up Zsh shell integration]'
  '--fish[Print script to set up Fish shell integration]'

  # Help
  '--version[Display version information and exit]'
  '--help[Show help message]'
  '--man[Show man page]'
)

_arguments -S $args
