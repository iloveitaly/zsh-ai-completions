#compdef fzf

local -a options

options=(
  '(-x --extended)'{-x,--extended}'[Extended-search mode]'
  '(+x --no-extended)'{+x,--no-extended}'[Disable extended-search mode]'
  '(-e --exact)'{-e,--exact}'[Enable Exact-match]'
  '(-i --ignore-case)'{-i,--ignore-case}'[Case-insensitive match]'
  '(+i --no-ignore-case)'{+i,--no-ignore-case}'[Case-sensitive match]'
  '--scheme=[Scoring scheme]:scheme:(default path history)'
  '--literal[Do not normalize latin script letters before matching]'
  '(-n --nth)'{-n,--nth}'=[Comma-separated list of field index expressions]:field index expressions'
  '--with-nth=[Transform the presentation of each line using field index expressions]:field index expressions'
  '(-d --delimiter)'{-d,--delimiter}'=[Field delimiter regex]:delimiter'
  '(+s --no-sort)'{+s,--no-sort}'[Do not sort the result]'
  '--tail=[Maximum number of items to keep in memory]:number'
  '--track[Track the current selection when the result is updated]'
  '--tac[Reverse the order of the input]'
  '--disabled[Do not perform search]'
  '--tiebreak=[Comma-separated list of sort criteria]:criteria:_values -s , criteria length chunk begin end index'
  '(-m --multi)'{-m,--multi}'[Enable multi-select with tab/shift-tab]'
  '--no-mouse[Disable mouse]'
  '--bind=[Custom key bindings]:key bindings'
  '--cycle[Enable cyclic scroll]'
  '--wrap[Enable line wrap]'
  '--wrap-sign=[Indicator for wrapped lines]:string'
  '--no-multi-line[Disable multi-line display of items when using --read0]'
  '--keep-right[Keep the right end of the line visible on overflow]'
  '--scroll-off=[Number of screen lines to keep above or below]:lines'
  '--no-hscroll[Disable horizontal scroll]'
  '--hscroll-off=[Number of screen columns to keep to the right]:columns'
  '--filepath-word[Make word-wise movements respect path separators]'
  '--jump-labels=[Label characters for jump mode]:characters'
  '--height=[Display fzf window below the cursor with the given height]:height'
  '--min-height=[Minimum height when --height is given in percent]:height'
  '--tmux=[Start fzf in a tmux popup]:options'
  '--layout=[Choose layout]:layout:(default reverse reverse-list)'
  '--border=[Draw border around the finder]:style:(rounded sharp bold block thinblock double horizontal vertical top bottom left right none)'
  '--border-label=[Label to print on the border]:label'
  '--border-label-pos=[Position of the border label]:position'
  '--margin=[Screen margin]:margin'
  '--padding=[Padding inside border]:padding'
  '--info=[Finder info style]:style:(default right hidden inline inline-right)'
  '--info-command=[Command to generate info line]:command'
  '--separator=[String to form horizontal separator on info line]:string'
  '--no-separator[Hide info line separator]'
  '--scrollbar=[Scrollbar character(s)]:characters'
  '--no-scrollbar[Hide scrollbar]'
  '--prompt=[Input prompt]:prompt'
  '--pointer=[Pointer to the current line]:pointer'
  '--marker=[Multi-select marker]:marker'
  '--marker-multi-line=[Multi-select marker for multi-line entries]:marker'
  '--header=[String to print as header]:header'
  '--header-lines=[The first N lines of the input are treated as header]:number'
  '--header-first[Print header before the prompt line]'
  '--ellipsis=[Ellipsis to show when line is truncated]:ellipsis'
  '--ansi[Enable processing of ANSI color codes]'
  '--tabstop=[Number of spaces for a tab character]:spaces'
  '--color=[Base scheme and/or custom colors]:color scheme:(dark light 16 bw)'
  '--highlight-line[Highlight the whole current line]'
  '--no-bold[Do not use bold text]'
  '--history=[History file]:file:_files'
  '--history-size=[Maximum number of history entries]:number'
  '--preview=[Command to preview highlighted line]:command'
  '--preview-window=[Preview window layout]:layout'
  '--preview-label=[Preview window label]:label'
  '--preview-label-pos=[Preview window label position]:position'
  '(-q --query)'{-q,--query}'=[Start the finder with the given query]:query'
  '(-1 --select-1)'{-1,--select-1}'[Automatically select the only match]'
  '(-0 --exit-0)'{-0,--exit-0}'[Exit immediately when there is no match]'
  '(-f --filter)'{-f,--filter}'=[Filter mode]:filter'
  '--print-query[Print query as the first line]'
  '--expect=[Comma-separated list of keys to complete fzf]:keys'
  '--read0[Read input delimited by ASCII NUL characters]'
  '--print0[Print output delimited by ASCII NUL characters]'
  '--sync[Synchronous search for multi-staged filtering]'
  '--with-shell=[Shell command and flags to start child processes with]:shell command'
  '--listen=[Start HTTP server to receive actions]:address and port'
  '--walker=[Directory traversal options]:options'
  '--walker-root=[Root directory from which to start walker]:directory:_directories'
  '--walker-skip=[Comma-separated list of directory names to skip]:directories'
  '--bash[Print script to set up Bash shell integration]'
  '--zsh[Print script to set up Zsh shell integration]'
  '--fish[Print script to set up Fish shell integration]'
  '--version[Display version information and exit]'
  '--help[Show this message]'
  '--man[Show man page]'
)

_arguments -s -S $options

