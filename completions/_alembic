#compdef alembic

function _alembic {
  local context state state_descr line
  typeset -A opt_args

  _arguments -S \
    '(-h --help)'{-h,--help}'[show this help message and exit]' \
    '--version[show program version number and exit]' \
    '(-c --config)'{-c,--config}'[Alternate config file]:config file:_files' \
    '(-n --name)'{-n,--name}'[Name of section in .ini file to use for Alembic config]:section name' \
    '*-x[Additional arguments consumed by custom env.py scripts]:argument' \
    '--raiseerr[Raise a full stack trace on error]' \
    '(-q --quiet)'{-q,--quiet}'[Do not log to std output]' \
    '1: :_alembic_commands' \
    '*:: :->args'

  case $state in
    args)
      case $words[1] in
        branches)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '(-v --verbose)'{-v,--verbose}'[Use more verbose output]'
          ;;
        check)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]'
          ;;
        current)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '(-v --verbose)'{-v,--verbose}'[Use more verbose output]'
          ;;
        downgrade)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '--sql[Do not emit SQL to database - dump to standard output/file instead]' \
            '--tag[Arbitrary tag name]:tag' \
            '1:revision'
          ;;
        edit)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '1:revision'
          ;;
        ensure_version)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '--sql[Do not emit SQL to database - dump to standard output/file instead]'
          ;;
        heads)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '(-v --verbose)'{-v,--verbose}'[Use more verbose output]' \
            '--resolve-dependencies[Treat dependency versions as down revisions]'
          ;;
        history)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '(-r --rev-range)'{-r,--rev-range}'[Specify a revision range (start:end)]:revision range' \
            '(-v --verbose)'{-v,--verbose}'[Use more verbose output]' \
            '(-i --indicate-current)'{-i,--indicate-current}'[Indicate the current revision]'
          ;;
        init)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '(-t --template)'{-t,--template}'[Setup template for use with init]:template' \
            '--package[Write empty __init__.py files to the environment and version locations]' \
            '1:directory:_files -/'
          ;;
        list_templates)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]'
          ;;
        merge)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '(-m --message)'{-m,--message}'[Message string to use with revision]:message' \
            '--branch-label[Specify a branch label to apply to the new revision]:branch label' \
            '--rev-id[Specify a hardcoded revision id instead of generating one]:revision id' \
            '*:revisions'
          ;;
        revision)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '(-m --message)'{-m,--message}'[Message string to use with revision]:message' \
            '--autogenerate[Populate revision script with candidate migration operations]' \
            '--sql[Do not emit SQL to database - dump to standard output/file instead]' \
            '--head[Specify head revision or <branchname> @head to base new revision on]:head' \
            '--splice[Allow a non-head revision as the head to splice onto]' \
            '--branch-label[Specify a branch label to apply to the new revision]:branch label' \
            '--version-path[Specify specific path from config for version file]:version path:_files' \
            '--rev-id[Specify a hardcoded revision id instead of generating one]:revision id' \
            '--depends-on[Specify one or more revision identifiers which this revision should depend on]:depends on'
          ;;
        show)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '1:revision'
          ;;
        stamp)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '--sql[Do not emit SQL to database - dump to standard output/file instead]' \
            '--tag[Arbitrary tag name]:tag' \
            '--purge[Unconditionally erase the version table before stamping]' \
            '*:revisions'
          ;;
        upgrade)
          _arguments -S \
            '(-h --help)'{-h,--help}'[show this help message and exit]' \
            '--sql[Do not emit SQL to database - dump to standard output/file instead]' \
            '--tag[Arbitrary tag name]:tag' \
            '1:revision'
          ;;
        *)
          _default
          ;;
      esac
      ;;
  esac
}

function _alembic_commands {
  local -a commands
  commands=(
    "branches:Show current branch points"
    "check:Check if revision command with autogenerate has pending upgrade ops"
    "current:Display the current revision for a database"
    "downgrade:Revert to a previous version"
    "edit:Edit revision script(s) using \$EDITOR"
    "ensure_version:Create the alembic version table if it doesn't exist already"
    "heads:Show current available heads in the script directory"
    "history:List changeset scripts in chronological order"
    "init:Initialize a new scripts directory"
    "list_templates:List available templates"
    "merge:Merge two revisions together"
    "revision:Create a new revision file"
    "show:Show the revision(s) denoted by the given symbol"
    "stamp:Stamp the revision table with the given revision"
    "upgrade:Upgrade to a later version"
  )
  _describe 'command' commands
}

_alembic "$@"
