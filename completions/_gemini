#compdef gemini

_gemini_mcp_add() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '(-s --scope)'{-s,--scope}'[Configuration scope]:scope:(user project)' \
    '(-t --transport --type)'{-t,--transport,--type}'[Transport type]:transport:(stdio sse http)' \
    '*'{-e,--env}'[Set environment variables]:env var (KEY=value)' \
    '*'{-H,--header}'[Set HTTP headers]:header' \
    '--timeout[Set connection timeout (ms)]:ms' \
    '--trust[Trust the server]' \
    '--description[Set description]:text' \
    '*--include-tools[Tools to include]:tools' \
    '*--exclude-tools[Tools to exclude]:tools' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '1:name: ' \
    '2:commandOrUrl: ' \
    '*::args: '
}

_gemini_mcp_remove() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '(-s --scope)'{-s,--scope}'[Configuration scope]:scope:(user project)' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '1:name: '
}

_gemini_mcp_list() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '(-h --help)'{-h,--help}'[Show help]'
}

_gemini_mcp() {
  local context state state_descr line
  typeset -A opt_args

  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '1: :_gemini_mcp_commands' \
    '*:: :->args'

  case $state in
    args)
      case $line[1] in
        add) _gemini_mcp_add ;;
        remove) _gemini_mcp_remove ;;
        list) _gemini_mcp_list ;;
        *) _message "command" ;;
      esac
      ;;
  esac
}

_gemini_mcp_commands() {
  local -a commands
  commands=(
    "add:Add a server"
    "remove:Remove a server"
    "list:List all configured MCP servers"
  )
  _describe -t commands 'gemini mcp command' commands
}

_gemini_extensions_install() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '--ref[The git ref to install from]:git ref' \
    '--auto-update[Enable auto-update for this extension]' \
    '--pre-release[Enable pre-release versions for this extension]' \
    '--consent[Acknowledge security risks and skip confirmation]' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '1:source:_files'
}

_gemini_extensions_uninstall() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '*:names: '
}

_gemini_extensions_list() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '(-h --help)'{-h,--help}'[Show help]'
}

_gemini_extensions_update() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '--all[Update all extensions]' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '1:name: '
}

_gemini_extensions_disable() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '--scope[The scope to disable the extension in]:scope:(User Workspace)' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '1:name: '
}

_gemini_extensions_enable() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '--scope[The scope to enable the extension in]:scope' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '1:name: '
}

_gemini_extensions_link() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '--consent[Acknowledge security risks and skip confirmation]' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '1:path:_files -/'
}

_gemini_extensions_new() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '1:path:_files -/' \
    '2:template:(context custom-commands exclude-tools mcp-server)'
}

_gemini_extensions_validate() {
  _arguments -S \
    '(-d --debug)'{-d,--debug}'[Run in debug mode]' \
    '(-h --help)'{-h,--help}'[Show help]' \
    '1:path:_files -/'
}

_gemini
